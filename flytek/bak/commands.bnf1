#BNF+IAT 1.0 UTF-8;
!grammar commands;
!slot <words>;
/******************************************** 一些公用的词 ****************************************/
!slot <ba>;
!slot <showMe>;
!slot <I>;
!slot <want>;
!slot <your>;
!slot <my>;
!slot <turnOn>;
!slot <turnOff>;
!slot <scan>;
/********************************************** 前后左右移动 **************************************/
!slot <always>;
!slot <direction>;
!slot <to>;
!slot <move>;
/********************************************** 抬头低头 ******************************************/
!slot <liftUpDown>;
!slot <head>;
/********************************************* 左右看 *********************************************/
!slot <turn>;
!slot <look>;
!slot <side>;
/********************************************* 断网联网 *******************************************/
!slot <cutOffOn>;
!slot <net>;
/********************************************* 唱歌 ***********************************************/
!slot <sing>;
!slot <oneSong>;
!slot <music>;
!slot <listen>;
/********************************************* 跳舞 ***********************************************/
!slot <jump>;
!slot <oneDance>;
!slot <dance>;
!slot <danceStart>;
!slot <danceEnd>;
/********************************************* 拍照 ***********************************************/
!slot <take>;
!slot <onePicture>;
!slot <picture>;
!slot <camera>;
/********************************************* 联系人 *********************************************/
!slot <contact>;
!slot <call>;
!slot <oneCall>;
!slot <phone>;
!slot <callTo>;
/**************************************** 空调、电视、投影 ****************************************/
!slot <air>;
!slot <TV>;
!slot <TY>;
/********************************************** 关机 **********************************************/
!slot <shutdown>;
/********************************************* 查看照片 *******************************************/
!slot <monthNum>;
!slot <monthName>;
!slot <dayNum>;
!slot <dayName>;
!slot <whatPhoto>;
!slot <photos>;
/********************************************* 多媒体视频 *****************************************/
!slot <medias>;
!slot <mediasUI>;
/********************************************* 多媒体音频 *****************************************/
!slot <arts>;
/********************************************* 打开游戏界面****************************************/
!slot <play>;
!slot <whatGame>;
!slot <game>;
/********************************************* 打开一些设置****************************************/
!slot <UI>;
!slot <qrCodeScan>;
!slot <qrCode>;
!slot <settings>;
!slot <wifi>;
!slot <bluetooth>;
!slot <about>;
!slot <rename>;
!slot <flyMode>;
!slot <netHot>;
!slot <display>;
!slot <sound>;
!slot <storage>;
!slot <battery>;
!slot <appStatus>;
!slot <location>;
!slot <language>;
!slot <date>;
!slot <reset>;
!slot <tools>;
!slot <myApp>;
/********************************************* 音量调节 *******************************************/
!slot <volume>;
!slot <setTo>;
!slot <pole>;
!slot <voiceUp>;
!slot <voiceDown>;
!slot <whatVolume>;
!slot <speak>;
!slot <several>;
!slot <ge>;
!start <commandStart>;

<commandStart>:
<words>|
/********************************************** 前后左右移动 **************************************/
[<always>]<to><direction><move>|
/********************************************** 抬头低头 ******************************************/
<liftUpDown>[<your>]<head>|
/********************************************* 左右看 *********************************************/
[<ba>][<your>]<head><to><direction>[<side>]<turn>|
[<ba>][<your>]<head><turn><to><direction>[<side>]|
<to><direction>[<side>]<turn><head>|
<to><direction>[<side>]<look>|
/********************************************* 断网联网 *******************************************/
<cutOffOn><net>|
/********************************************* 唱歌 ***********************************************/
[<showMe>]<sing>[<oneSong>][<music>]|
<sing>[<oneSong>]<music><showMe><listen>|
[<I>][<want>]<listen>[<oneSong>]<music>|
/********************************************* 跳舞 ***********************************************/
<jump>[<oneDance>]<dance>|
[<showMe>]<jump>[<oneDance>]<dance>|
[<I>][<want>]<look><jump><dance>|
<jump>[<oneDance>]<dance><showMe><look>|
<jump>[<oneDance>]<dance>[<danceEnd>]|
<danceStart><jump>[<oneDance>]<dance>|
/********************************************* 拍照 ***********************************************/
[<showMe>]<take>[<onePicture>]<picture>|
[<I>][<want>]<take>[<onePicture>]<picture>|
[<showMe>]<turnOn><camera>|
[<ba>]<camera><turnOn>|
/********************************************* 联系人 *********************************************/
[<I>][<want>]<callTo><contact><call>[<oneCall>]<phone>|
[<I>][<want>]<call>[<oneCall>]<phone><callTo><contact>|
[<I>][<want>]<call>[<oneCall>]<phone>|<ba><phone><call>|
/**************************************** 空调、电视、投影 ****************************************/
[<ba>]<air><turnOn>|<turnOn><air>|
[<ba>]<air><turnOff>|<turnOff><air>|
[<ba>]<TV><turnOn>|<turnOn><TV>|
[<ba>]<TV><turnOff>|<turnOff><TV>|
[<ba>]<TY><turnOn>|<turnOn><TY>|
[<ba>]<TY><turnOff>|<turnOff><TY>|
/********************************************** 关机 **********************************************/
[<I>][<want>]<shutdown>|
[<showMe>]<shutdown>|
/********************************************* 查看照片 *******************************************/
[<showMe>]<turnOn>[<monthNum><monthName>][<dayNum><dayName>][<whatPhoto>]<photos>|
[<showMe>]<scan>[<monthNum><monthName>][<dayNum><dayName>][<whatPhoto>]<photos>|
[<ba>][<monthNum><monthName>][<dayNum><dayName>][<whatPhoto>]<photos><turnOn>|
[<I>][<want>]<turnOn>[<monthNum><monthName>][<dayNum><dayName>][<whatPhoto>]<photos>|
[<I>][<want>]<scan>[<monthNum><monthName>][<dayNum><dayName>][<whatPhoto>]<photos>|
<turnOn><my><photos>|
<scan><my><photos>|
/********************************************* 多媒体视频 *****************************************/
[<ba>]<medias>[<mediasUI>]<turnOn>|
[<I>][<want>]<turnOn><medias>[<mediasUI>]|
[<showMe>]<turnOn><medias>[<mediasUI>]|
[<I>][<want>]<look><medias>[<mediasUI>]|
/********************************************* 多媒体音频 *****************************************/
[<ba>]<arts>[<mediasUI>]<turnOn>|
[<I>][<want>]<turnOn><arts>[<mediasUI>]|
[<showMe>]<turnOn><arts>[<mediasUI>]|
[<I>][<want>]<listen>[<oneSong>]<arts>|
[<showMe>]<sing>[<oneSong>]<arts>|
/********************************************* 打开游戏界面****************************************/
[<ba>][<whatGame>]<game>[<UI>]<turnOn>|
[<I>][<want>]<turnOn>[<whatGame>]<game>[<UI>]|
[<I>][<want>]<play>[<whatGame>]<game>|
/********************************************* 打开一些设置****************************************/
[<qrCodeScan>]<qrCode>[<UI>]|
[<ba>]<qrCode>[<UI>]<turnOn>|[<turnOn>]<qrCode>[<UI>]|
[<ba>]<settings>[<UI>]<turnOn>|<turnOn><settings>[<UI>]|
[<ba>]<wifi>[<UI>]<turnOn>|<turnOn><wifi>[<UI>]|
[<ba>]<bluetooth>[<UI>]<turnOn>|<turnOn><bluetooth>[<UI>]|
[<ba>]<about>[<UI>]<turnOn>|<turnOn><about>[<UI>]|
[<ba>]<rename>[<UI>]<turnOn>|<turnOn><rename>[<UI>]|
[<ba>]<flyMode>[<UI>]<turnOn>|<turnOn><flyMode>[<UI>]|
[<ba>]<netHot>[<UI>]<turnOn>|<turnOn><netHot>[<UI>]|
[<ba>]<display>[<UI>]<turnOn>|<turnOn><display>[<UI>]|
[<ba>]<sound>[<UI>]<turnOn>|<turnOn><sound>[<UI>]|
[<ba>]<storage>[<UI>]<turnOn>|<turnOn><storage>[<UI>]|<scan><storage>[<UI>]|
[<ba>]<battery>[<UI>]<turnOn>|<turnOn><battery>[<UI>]|<scan><battery>[<UI>]|
[<ba>]<appStatus>[<UI>]<turnOn>|<turnOn><appStatus>[<UI>]|<scan><appStatus>[<UI>]|
[<ba>]<location>[<UI>]<turnOn>|<turnOn><location>[<UI>]|
[<ba>]<language>[<UI>]<turnOn>|<turnOn><language>[<UI>]|
[<ba>]<date>[<UI>]<turnOn>|<turnOn><date>[<UI>]|
[<ba>]<reset>[<UI>]<turnOn>|<turnOn><reset>[<UI>]|
/********************************************* 打开工具界面****************************************/
[<ba>][<my>]<tools><turnOn>|
[<I>][<want>]<turnOn>[<my>]<tools>|
[<I>][<want>]<scan>[<my>]<tools>|
/********************************************* 打开我的应用****************************************/
[<ba>]<myApp><turnOn>|
[<I>][<want>]<turnOn><myApp>|
/********************************************* 音量调节 *******************************************/
[<ba>]<volume><setTo><pole>|
[<ba>]<volume><voiceUp>[<whatVolume>]|
[<ba>]<volume><voiceDown>[<whatVolume>]|
<voiceUp>[<whatVolume>]<volume>|
<voiceDown>[<whatVolume>]<volume>|
<voiceUp><volume>[<whatVolume>]|
<voiceDown><volume>[<whatVolume>]|
<speak><volume><voiceUp>[<whatVolume>]|
<speak><volume><voiceDown>[<whatVolume>]|
<speak><voiceUp>[<whatVolume>]<volume>|
<speak><voiceDown>[<whatVolume>]<volume>|
<voiceUp><several><ge><volume>|
<voiceDown><several><ge><volume>|
[<ba>]<volume><voiceUp><several><ge>|
[<ba>]<volume><voiceDown><several><ge>;


<words>:过来;
/******************************************** 一些公用的词 ****************************************/
<ba>:把;
<showMe>:给我|帮我|给我们|帮我们;
<I>:我;
<want>:想|要|想要;
<your>:你的;
<my>:我的;
<turnOn>:开|打开|开启|启动|显示|呈现;
<turnOff>:关|关闭|关掉|关了|关上;
<scan>:看|查看|提取|显示|浏览|进入;
/******************************************** 前后左右移动 ****************************************/
<always>:一直;
<direction>:前|后|左|右;
<to>:向|往|朝;
<move>:走|跑|转|移动|行走|退;
/******************************************** 抬头低头 ********************************************/
<liftUpDown>:抬|抬起|低|低下;
<head>:头;
/******************************************** 左右看 **********************************************/
<turn>:转;
<look>:看|瞅;
<side>:边|面;
/******************************************** 断网联网 ********************************************/
<cutOffOn>:断|断开|连|连接;
<net>:网|网络;
/******************************************** 唱歌 ************************************************/
<sing>:唱|播|放|播放|来;
<oneSong>:首|一首|支|一支|个|一个;
<music>:歌|曲|歌曲|音乐;
<listen>:听;
/********************************************* 跳舞 ***********************************************/
<danceStart>:请|你|请你|请你跳|让你|快|跳|跳舞;
<jump>:跳|来|秀;
<oneDance>:个|一个|支|一支|段|一段|下|一下;
<dance>:舞;
<danceEnd>:来看|好不好|怎么样|咋样;
/********************************************* 拍照 ***********************************************/
<take>:拍;
<onePicture>:个|一个|张|一张;
<picture>:照|照片;
<camera>:相机|照相机;
/********************************************* 联系人 *********************************************/
<contact>:10086;
<call>:打|拨|拨打|打打|打开;
<oneCall>:个|一个|通|一通;
<phone>:电话;
<callTo>:给|到;
/**************************************** 空调、电视、投影 ****************************************/
<air>:空调;
<TV>:电视;
<TY>:投影|投影仪;
/********************************************* 关机 ***********************************************/
<shutdown>:关机|你关机;
/********************************************* 查看照片 *******************************************/
<monthNum>:1|2|3|4|5|6|7|8|9|10|11|12;
<monthName>:月|月份;
<dayNum>:1|2|3|4|5|6|7|8|9|10|11|12|13|14|15|16|17|18|19|20|21|22|23|24|25|26|27|28|29|30|31;
<dayName>:日|号;
<whatPhoto>:拍的|照的|的;
<photos>:相册|照片|相片;
/********************************************* 多媒体视频 *****************************************/
<medias>:电视剧|电影|动漫|综艺|体育|直播;
<mediasUI>:节目|界面|节目界面;
/********************************************* 多媒体音频 *****************************************/
<arts>:戏曲|儿歌|文学|电台|音乐界面;
/********************************************* 打开游戏界面****************************************/
<play>:玩;
<whatGame>:体感|手柄;
<game>:游戏;
/********************************************* 打开一些设置****************************************/
<UI>:设置|界面|设置界面;
<qrCodeScan>:扫|扫一扫|查看|下载|绑定;
<qrCode>:二维码;
<settings>:机器人;
<wifi>:WIFI|无线网络|无线|网络;
<bluetooth>:蓝牙;
<about>:关于|关于机器人;
<rename>:修改名字|修改机器人名字;
<flyMode>:飞行模式;
<netHot>:网络热点;
<display>:显示;
<sound>:提示音|音量;
<storage>:存储状况|存储信息;
<battery>:电池电量;
<appStatus>:应用状况|应用信息;
<location>:位置信息;
<language>:语言;
<date>:日期|时间|日期和时间;
<reset>:重置|出厂;
/********************************************* 打开工具界面****************************************/
<tools>:下载|信息|短信|录音机|文件管理|日历|时钟|浏览器|通讯录;
/********************************************* 打开我的应用****************************************/
<myApp>:应用|我的应用;
/********************************************* 音量调节 *******************************************/
<volume>:声|声音|音量;
<setTo>:调到;
<pole>:最高|最低|最大|最小;
<voiceUp>:增加|增大|调大|调高|加大|大|响;
<voiceDown>:减少|减小|减|调小|调低|降低|降|低|小;
<whatVolume>:点|一点|些|一些;
<speak>:说话|讲话;
<several>:1|两|2|3|4|5|6|7|8|9|10|11|12|13|14|15|16|17|18|19|20|21|22|23|24|25|26|27|28|29|30|31|32|33|34|35|36|37|38|39|40|41|42|43|44|45|46|47|48|49|50;
<ge>:格;
