apply plugin: 'com.android.library'
apply from: '../buildshell/library.gradle'

def mic_ver = ""
def app_id = ""
def platform = ""
def wakeup = ""
if(project.hasProperty("config") && file(project.property("config")).exists()) {
    Properties props = new Properties()
    props.load(new FileInputStream(file(project.property("config"))))
    mic_ver = props['mic_ver']
    app_id = props['app_id']
    platform = props['platform']
    wakeup = props['wakeup']
}

android{
    sourceSets.main {
        //aidl.srcDirs = ['src/main/aidl']
        java.srcDirs = ["src/main/java",
                        "src/main/" + mic_ver + "/"+ platform + "/java"]
        res.srcDir "src/main/res"
        //disable automatic ndk-build call
        jni.srcDirs = []
        jniLibs.srcDirs = ['src/main/libs',
                           'src/main/' + mic_ver + '/'+ platform + '/libs',
                           'src/main/ext_res/'+ app_id + '/libs']
        manifest.srcFile "src/main/AndroidManifest.xml"
        assets.srcDirs = ["src/main/assets", "src/main/"+ mic_ver + "/wakeup/" + wakeup + "/" + "assets"]
        resources.srcDirs = ['src/main/resources/']
    }
}

def getLayoutLibPath() {
    return "${android.getSdkDirectory().getAbsolutePath()}" + "/platforms/" +
            android.compileSdkVersion + "/data/layoutlib.jar"
}

//compile 'group:module:version'
dependencies {
    provided files(getLayoutLibPath())
    compile fileTree(include: ['*.jar'], dir: 'src/main/libs')
    compile fileTree(dir: "src/main/" + mic_ver + "/" + platform + "/libs", include: ['*.jar'])
    compile fileTree(dir: "src/main/ext_res/" + app_id + "/libs", include: ['*.jar'])

    def presentationTest = rootProject.ext.presentationTest
    androidTestCompile(presentationTest.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    def dataTest = rootProject.ext.dataTest
    testCompile dataTest.junit

    def publicDepend = rootProject.ext.publicDepend
    compile publicDepend.support
    compile publicDepend.appcompat

    compile project(':tools')
    compile project(':drive')
    compile project(':plugin')
}
